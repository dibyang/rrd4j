buildscript {
  repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenCentral()
  }
  dependencies {
    classpath 'net.researchgate:gradle-release:2.6.0'
  }
  //skip Test tasks
  gradle.taskGraph.whenReady {
    tasks.each { task ->
      if (task.name.contains("test")) {
        task.enabled = false
      }
    }
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

File signing_cfg = new File("$project.rootDir/signing.properties")
if(signing_cfg.exists()) {
  Properties props = new Properties()
  props.load(new FileInputStream(signing_cfg))
  props.each { prop ->
    project.ext.set(prop.key, prop.value)
  }

  // 将构建的包签名
  signing {
    sign configurations.archives
  }
}

release {
  failOnUnversionedFiles = true
  failOnCommitNeeded = true
  failOnPublishNeeded = true
  failOnSnapshotDependencies = false
  failOnUpdateNeeded = true
  revertOnFail = true
  tagTemplate = 'v${version}'
  scmAdapters = [
      net.researchgate.release.GitAdapter
  ]

  git {
    requireBranch = 'master'
    pushToRemote = 'origin'
    pushToBranchPrefix = ''
    commitVersionFileOnly = false
    signTag = false
  }

}


afterReleaseBuild.dependsOn uploadArchives


configurations.all {
  resolutionStrategy {
    // cache dynamic versions for 10 seconds
    cacheDynamicVersionsFor 10, 'seconds'
    // don't cache changing modules at all
    cacheChangingModulesFor 0, 'seconds'
  }
}

sourceCompatibility = project.jdkVersion
targetCompatibility = project.jdkVersion


repositories {
  mavenLocal()
  maven { url 'https://maven.aliyun.com/repository/public/' }
  mavenCentral()
}

dependencies {
    implementation 'com.sleepycat:je:18.3.12'
    implementation 'org.mongodb:mongo-java-driver:3.12.10'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.easymock:easymock:4.3'
    testImplementation 'org.ops4j.pax.exam:pax-exam-container-native:4.13.4'
    testImplementation 'org.ops4j.pax.exam:pax-exam-junit4:4.13.4'
    testImplementation 'org.ops4j.pax.exam:pax-exam-link-mvn:4.13.4'
    testImplementation 'org.apache.felix:org.apache.felix.framework:7.0.1'
    testImplementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
}

java {
  withSourcesJar()
  withJavadocJar()
}

// javadoc 配置，这里是自定义了 java doc 的一些配置
javadoc {
  description = "Generates project-level javadoc for use in -javadoc jar"

  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
  options.author = true
  options.version = true
  options.header = project.name
  options.addStringOption('Xdoclint:none', '-quiet')

  // suppress warnings due to cross-module @see and @link references;
  // note that global 'api' task does display all warnings.
  logging.captureStandardError LogLevel.INFO
  logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
  options.encoding = "UTF-8"  //编码一定要配置否则直接出错
  options.charSet = 'UTF-8'
}

// 并将它们与项目 jar 本身一起连接到 artifacts 集合中：
artifacts {
  archives javadocJar, sourcesJar
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: findProperty("ossrhUsername")?:"", password: findProperty("ossrhPassword")?:"")
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: findProperty("ossrhUsername")?:"", password: findProperty("ossrhPassword")?:"")
      }

      pom.project {
        name 'rrd4j'
        packaging 'jar'
        // optionally artifactId can be defined here
        description project.description
        url 'https://github.com/dibyang/rrd4j'

        scm {
          connection 'scm:git:https://github.com/dibyang/rrd4j.git'
          developerConnection 'scm:git:https://github.com/dibyang/rrd4j.git'
          url 'https://github.com/dibyang/rrd4j.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'yangzj'
            name 'Yang zhijian'
            email '3227817@qq.com'
          }
        }
      }
    }
  }
}